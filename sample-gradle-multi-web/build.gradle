/*
 * Warを作成しWebサーバにデプロイする。（開発の場合はテストも実施）
 * ・実行タスク（開発）：clean assemble cargoStartLocal check cargoStopLocal
 * ・実行タスク（本番）：clean assemble cargoUndeployRemote cargoDeployRemote
 *
 */
plugins {
  id 'war'
  id 'eclipse-wtp'
  id 'jacoco'
  id 'com.bmuschko.cargo' version '2.4'
}

ext {
    localWebServerHome = new File(rootProject.projectDir, "web-server/$webServerHome")
    localWebServerLibDir = new File(localWebServerHome, webServerLibDir)
}

dependencies {

    compile project(':sample-gradle-multi-shared')

    compile group: 'com.codeborne', name: 'selenide', version: '4.14.2'
    providedCompile fileTree(dir: localWebServerLibDir, include: 'java*/**/*.jar')

    def cargoVersion = '1.4.5'
    cargo "org.codehaus.cargo:cargo-core-uberjar:${cargoVersion}",
          "org.codehaus.cargo:cargo-ant:${cargoVersion}"
}

eclipse {

    wtp {
        component {
            contextPath  = contextPath
        }
    }
}

cargo {

    containerId = cargoContainerId
    port = Integer.parseInt(cargoPort)

    deployable {
        // ページを表示するためのURL：http://ホスト名/contextPath/ページ名
        //   例：http://localhost:8080/SampleWebApp/login.xhtml
        context = contextPath
    }

    local {

        homeDir = localWebServerHome
        timeout = 10000

        def jacocoAagentJar = new File("${jacocoDir}/jacocoagent.jar").path
        def jacocoReport = new File(reportsDir, 'jacoco/jacoco.exec').path

        jvmArgs = "-Xms64m -Xmx512m -XX:MaxPermSize=256m -Djava.net.preferIPv4Stack=true "
        jvmArgs += "-javaagent:${jacocoAagentJar}=destfile=${jacocoReport},append=false,excludes=**/Test*.class:**/*Test.class"
    }

    remote {

        hostname = cargoHostName
        username = cargoHostUser
        password = cargoHostPassword
    }
}
