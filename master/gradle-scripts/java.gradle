// 拡張プロパティ
ext{
    // プロジェクト一覧
    sharedProjects = getProjects(keywordsSharedProject) // 共有プロジェクト
    batchProjects = getProjects(keywordsBatchProject) // バッチプロジェクト
    webProjects = getProjects(keywordsWebProject) // オンラインプロジェクト
    bizBatchProjects = batchProjects - sharedProjects // 業務バッチプロジェクト
    bizWebProjects =  webProjects - sharedProjects // 業務オンラインプロジェクト
    bizProjects = bizBatchProjects + bizWebProjects // 業務プロジェクト

    // マスタープロジェクトのリソースディレクトリ
    masterMiscDir = file('misc')
    checkstyleDir = file("${masterMiscDir}/checkstyle")
    jacocoDir= file("${masterMiscDir}/jacoco")
}

// 全プロジェクト共通
allprojects {

    apply plugin: 'java-library'

    // 設定
    configurations.all {

        // 依存関係のローカルキャッシュ無効化（「changing: true」を対象の依存関係の付与する）
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    // 依存関係
    dependencies {

    }

    repositories {

        mavenCentral() // セントラルMavenリポジトリ
        maven { url innerMavenRepoUrl } // 社内Mavenリポジトリ
    }
}

// サブプロジェクト共通
subprojects{

    group = "${baseGroupId}.${groupId}"
    version = branchName

    compileJava {

        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.encoding = encoding
    }

    compileTestJava {

        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.encoding = encoding
    }

    // 依存関係
    dependencies {

        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    }
}

// 業務プロジェクト共通
configure(bizProjects) {

    ext {
        // 依存する共有プロジェクト
        dependProjects = getProjects(dependProjectNames)
    }

    // 依存関係
    dependencies {

        dependProjects.name.each{ implementation project(":${it}") } // 共有プロジェクト
        implementation files("${deployRootDir}/${confDir}") // 外部リソースのクラスパス
    }
}

// 業務バッチプロジェクト共通
configure(bizProjects) {

    // 依存関係
    dependencies {

    }
}

// 業務Webプロジェクト共通
configure(bizWebProjects) {

    apply plugin: 'war'
    apply plugin: 'eclipse-wtp'

    ext {
        // ローカルWebサーバ情報
        localWebServerHome = localWebServerDir == ''
            ? new File(rootProject.projectDir, "web-server/${webServerHome}")
            : new File("${localWebServerDir}/${webServerHome}")

        localWebServerLibDir = new File(localWebServerHome, webServerLibDir)
    }

    // コンテキストパス
    eclipse.wtp.component { contextPath  = contextPath }

    // 依存関係
    dependencies {

        providedCompile fileTree(dir: localWebServerLibDir, include: 'java*/**/*.jar')
    }
}

// 対象のプロジェクト一覧を取得
def getProjects(keywords) {

    return subprojects.findAll {

        def projectName = it.name
        keywords.split().any { projectName.contains(it) }
    }
}
