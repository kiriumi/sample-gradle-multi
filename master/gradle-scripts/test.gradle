/*
 * 各種テストを行う
 * ・実行タスク：check
 *
 */
subprojects {

    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'

    ext {
        // JaCoCoレポートファイルの出力先
        jacocoReportFile = file("${reportsDir}/jacoco/jacoco.exec")
    }

    // 依存関係
    dependencies {

        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation group: 'org.dbunit', name: 'dbunit', version: '2.5.4'
        testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.18.0'

        if(project.plugins.hasPlugin('war')) { testImplementation group: 'com.codeborne', name: 'selenide', version: '4.14.2' }
    }

    checkstyle {

        toolVersion = '8.13' // Gradleラッパーの場合、バージョンを指定しないとエラーになる
        configFile = file("${checkstyleDir}/${checkstyleConfigFile}")
        ignoreFailures = true
    }

    checkstyleTest.excludes = ['**/*']

    spotbugs {

        ignoreFailures = true
    }

    spotbugsTest.excludes = ['**/*']

    pmd {

        ignoreFailures = true
    }

    pmdTest.excludes = ['**/*']

    test {

        jacoco {

            append = false
            excludes = ['**/*Test*']
            destinationFile = jacocoReportFile
        }
    }
}
