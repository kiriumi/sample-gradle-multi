/*
 * 各種テストを行う
 * ・実行タスク：check
 *
 */
subprojects {

    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    dependencies {

        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation group: 'org.dbunit', name: 'dbunit', version: '2.5.4'
        testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.18.0'

        if(project.plugins.hasPlugin('war')) { testImplementation group: 'com.codeborne', name: 'selenide', version: '4.14.2' }
    }

    checkstyle {

        toolVersion = '8.13' // Gradleラッパーの場合、バージョンを指定しないとエラーになる
        configFile = file("${checkstyleDir}/${checkstyleConfigFile}")
    }

    // 検査結果が失敗でも、ビルドは成功させる
    checkstyle.ignoreFailures = true
    spotbugs.ignoreFailures = true
    pmd.ignoreFailures = true

    // テストコードは検査対象外
    checkstyleTest.excludes = ['**/*']
    spotbugsTest.excludes = ['**/*']
    pmdTest.excludes = ['**/*']

    test.jacoco {

        append = false
        excludes = ['**/*Test*']
    }
}
