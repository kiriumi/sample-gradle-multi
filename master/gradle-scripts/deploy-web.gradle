/*
 * WarをWebサーバにデプロイする（開発の場合はテストも実施）
 * ・実行タスク（開発）：clean assemble cargoStartLocal check cargoStopLocal
 * ・実行タスク（本番）：clean assemble cargoUndeployRemote cargoDeployRemote
 *
 */
configure(bizWebProjects) {

    apply plugin: "com.bmuschko.cargo"

    dependencies {

        def cargoVersion = '1.4.5'
        cargo "org.codehaus.cargo:cargo-core-uberjar:${cargoVersion}",
              "org.codehaus.cargo:cargo-ant:${cargoVersion}"
    }

    cargo {

        containerId = cargoContainerId
        port = Integer.parseInt(cargoPort)

        deployable {

            context = contextPath
        }

        local {

            homeDir = localWebServerHome

            def jacocoAagentJar = new File("${jacocoDir}/jacocoagent.jar").path

            jvmArgs = '-Xms64m -Xmx512m -XX:MaxPermSize=256m -Djava.net.preferIPv4Stack=true '
            jvmArgs += "-javaagent:${jacocoAagentJar}=destfile=${jacocoReportFile.path},append=false,excludes=**/Test*.class:**/*Test.class"
        }

        remote {

            hostname = cargoHostName
            username = cargoHostUser
            password = cargoHostPassword
        }
    }
}
