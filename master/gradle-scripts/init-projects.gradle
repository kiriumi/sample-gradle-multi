/*
 * プロジェクトを初期化する
 * ・実行タスク：cleanEclipse eclipse initSubrojects
 *
 */
allprojects {

    apply plugin: 'eclipse'

    eclipse {

        // .projectの設定
        project {

            name = project.name
            buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
            natures 'org.eclipse.buildship.core.gradleprojectnature'
        }
    }

    eclipseJdt { doLast {

        // 文字コード
        file('.settings/org.eclipse.core.resources.prefs').write("""
            encoding/<project>=${encoding}
            """.stripIndent())

         // 改行コード
         file('.settings/org.eclipse.core.runtime.prefs').write("""
            line.separator=${lineSeparator}
            """.stripIndent())

         // Buildship
         file('.settings/org.eclipse.buildship.core.prefs').write("""
            connection.project.dir=
            override.workspace.settings=true
            connection.gradle.distribution=GRADLE_DISTRIBUTION(WRAPPER)
            gradle.user.home=
            offline.mode=false
            build.scans.enabled=false
            """.stripIndent())
    }}
}

subprojects {

    ext {
        // パッケージのパス
        packagePath = group.replace('.', '/')

        // UT以外の環境名
        envAfterUt = environments.split() - [envUt]
    }

    // ソースディレクトリの作成タスク
    task makeSourceSetsDirs doLast {

        // javaのソースディレクトリ作成（パッケージを含む）
        def makePackages = { javaSet ->

            javaSet.srcDirs.each { makeDirIfNotExist(new File(it, packagePath)) }
        }

        // resoucesのソースディレクトリ作成
        def makeResourceDir = { resourceSet ->

            resourceSet.srcDirs.each { makeDirIfNotExist(it) }
        }

        makePackages sourceSets.main.java
        makeResourceDir sourceSets.main.resources
        makePackages sourceSets.test.java
        makeResourceDir sourceSets.test.resources
    }

    // 各種ディレクトリの作成タスク
    task makeMiscDirs doLast {

        // DBマイグレーションSQLのディレクトリ
        makeDirIfNotExist(new File(projectDir, dbMigreSqlDir))

        // 外部リソースのディレクトリ
        makeDirIfNotExist(new File(projectDir, "${deployRootDir}/${shellDir}"))
        makeDirIfNotExist(new File(projectDir, "${deployRootDir}/${confDir}"))

        // 各環境の外部リソースのディレクトリ
        envAfterUt.each {

             makeDirIfNotExist(new File(projectDir, "${deployRootDir}/${shellDir}_${it}"))
             makeDirIfNotExist(new File(projectDir, "${deployRootDir}/${confDir}_${it}"))
        }
    }

    task initSubprojects(dependsOn: [makeSourceSetsDirs, makeMiscDirs])
}

// ディレクトリ作成メソッド
def makeDirIfNotExist(File dir) {

     if(!dir.exists()) { dir.mkdirs() }
}
