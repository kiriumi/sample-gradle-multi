/*
 * 共有プロジェクトをビルドし、Mavenリポジトリにアップロードする
 * ・実行タスク：clean build afterBuild uploadArchives
 *
 */
// 共有プロジェクト共通
configure(sharedProjects) {

    apply plugin: 'maven'

    // 設定
    configurations {
        deployerJars
    }

    // 依存関係
    dependencies {

        deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
    }

    // ソースのJar作成
    task sourcesJar(type: Jar) {

        classifier = 'sources'
        from sourceSets.main.allJava
     }

    // JavadocのJar作成
    task javadocJar(type: Jar) {

        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // Mavenリポジトリへのアップロード対象に含める
    artifacts {

        archives sourcesJar
        archives javadocJar
    }

    // Mavenリポジトリへのアップロード
    uploadArchives {

        dependsOn = [sourcesJar, javadocJar]

        repositories.mavenDeployer {

            // ローカルのMavenリポジトリ
            repository(url: innerMavenRepoUrl)

            configuration = configurations.deployerJars

            if(!innerMavenRepoUploadUrl.trim().isEmpty()) {

                 // 社内Mavenリポジトリ
                repository(url: innerMavenRepoUploadUrl) {

                    authentication(
                        userName: innerMavenRepoUser,
                        password: innerMavenRepoPassword)
                }
            }
        }
    }
}
