// 拡張プロパティ
ext{
    // プロジェクト一覧
    sharedProjects = getProjectNames(containsStrsShared) // 共有プロジェクト
    batchProjects = getProjectNames(containsStrsBatch) // バッチプロジェクト
    onlineProjects = getProjectNames(containsStrsOnline) // オンラインプロジェクト
    batchBizProjects = batchProjects - sharedProjects // 業務バッチプロジェクト
    onlineBizProjects =  onlineProjects - sharedProjects // 業務オンラインプロジェクト
    bizProjects = batchBizProjects + onlineBizProjects // 業務プロジェクト

    // マスタープロジェクトのリソースディレクトリ
    masterResourcesDir = file('resources')
    checkstyleDir = file("${masterResourcesDir}/checkstyle")
    jacocoDir= file("${masterResourcesDir}/jacoco")
}

// 全プロジェクト共通
allprojects {

    apply plugin: 'java'

    // 設定
    configurations.all {
        // 依存関係のローカルキャッシュ無効化（「changing: true」を対象の依存関係の付与する）
        //   例：compile group: （中略） version: '2.11.1', changing: true
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    // 依存関係
    dependencies {
        // DBマイグレーションのDBクリーンタスクが使用するため、全プロジェクト共通
        compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    }

    repositories {

        mavenCentral() // セントラルMavenリポジトリ
        maven { url innerMavenRepoUrl } // 社内Mavenリポジトリ
        mavenLocal() // ローカルMavenリポジトリ
    }
}

// サブプロジェクト共通
subprojects{

    group = "${baseGroupId}.${groupId}"
    version = "${branchName}.${environment}"

    def javaVersion = '1.8'
    def encoding = 'UTF-8'

    compileJava {

        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.encoding = encoding
    }

    compileTestJava {

        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.encoding = encoding
    }

    // 依存関係
    dependencies {

        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    }
}

// 業務プロジェクト共通
bizProjects.each { project(":${it}") {

    // 依存関係
    dependencies {

        runtime files("${deployRootDir}/${confDir}") // 外部リソースのクラスパス
    }
}}

// 業務バッチプロジェクト共通
batchBizProjects.each { project(":${it}") {

    // 依存関係
    dependencies {

        compile project(':sample-gradle-multi-shared')
    }
}}

// 業務Webプロジェクト共通
onlineBizProjects.each { project(":${it}") {

    apply plugin: 'war'
    apply plugin: 'eclipse-wtp'

    dependencies {

        compile project(':sample-gradle-multi-shared')

        testCompile group: 'com.codeborne', name: 'selenide', version: '4.14.2'
    }
}}

// 対象のプロジェクト名一覧を取得
def getProjectNames(containsStrs) {

    return subprojects.name.findAll {

        def projectName = it
        containsStrs.split().any { projectName.contains(it) }
    }
}
