// 外部プラグインの適用
plugins {
  id 'com.github.spotbugs' version '1.6.4'
  id 'org.flywaydb.flyway' version '5.1.4'
  id 'org.hidetake.ssh' version '2.9.0'
}

// 拡張プロパティ
ext{
    // 設定ディレクトリ
    masterResourcesDir = file('resources')
    checkstyleDir = file("${masterResourcesDir}/checkstyle")
    jacocoDir= file("${masterResourcesDir}/jacoco")

    // ローカルWebサーバのルートディレクトリ
    localWebServerRootDir = file('web-server')

    // flywayのタスククラス
    // ※拡張プロパティを通さないと、外部Gradleスクリプト側でクラスと認識されない
    flywayCleanTask = org.flywaydb.gradle.task.FlywayCleanTask
    flywayMigrateTask = org.flywaydb.gradle.task.FlywayMigrateTask
}

// 外部Gradleスクリプトの適用
def gradleScriptDir = 'gradle-script'
apply from: "${gradleScriptDir}/init-subprojects.gradle" // サブプロジェクトの初期化
apply from: "${gradleScriptDir}/db-migration.gradle" // DBマイグレーション
apply from: "${gradleScriptDir}/build-shared.gradle" // 共有プロジェクトのビルド
apply from: "${gradleScriptDir}/test.gradle" // テスト
apply from: "${gradleScriptDir}/deploy-batch.gradle" // デプロイ

// 全プロジェクト共通
allprojects {

    tasks.withType(JavaCompile){ options.encoding = 'UTF-8' }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url innerMavenRepoUrl }
    }
}

// サブプロジェクト共通
// ※JDBCの依存関係はDBマイグレーションのGradleスクリプトで定義すること
subprojects{

    group = groupId
    version = branchName + '.' + environment

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    // 依存関係
    dependencies {

        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'

        // 外部リソースのクラスパス
        runtime files("${deployRootDir}/${confDir}")
    }
}
