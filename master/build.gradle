/*
 * ビルドスクリプトを統括
 * ・実行タスク（CI）：clean cleanDb migrateDb assemble cargoStartLocal check cargoStopLocal afterBuild uploadArchives
 * ・実行タスク（リリース）：clean migrateDb assemble afterBuild uploadArchives deployBatch cargoUndeployRemote cargoDeployRemote commitDeploied tagDeploied
 */
// 外部プラグインの適用
buildscript {

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    // cargoプラグインのみ、こちらで宣言しないとコンテナIDの未定義エラーになる
    dependencies { classpath 'com.bmuschko:gradle-cargo-plugin:2.4'}
}

plugins {

  id 'com.github.spotbugs' version '1.6.4' // 静的解析
  id 'org.flywaydb.flyway' version '5.1.4' // DBマイグレーション
  id 'org.hidetake.ssh' version '2.9.0' // SSH
  id 'at.bxm.svntools' version '2.2.1' // SVN
}

// 拡張プロパティ
ext{
    // flywayのタスククラス
    // ※拡張プロパティを通さないと、外部Gradleスクリプト側でクラスと認識されない
    flywayCleanTask = org.flywaydb.gradle.task.FlywayCleanTask
    flywayMigrateTask = org.flywaydb.gradle.task.FlywayMigrateTask

    // SVNのタスククラス
    // ※拡張プロパティを通さないと、外部Gradleスクリプト側でクラスと認識されない
    svnCommitTask = at.bxm.gradleplugins.svntools.tasks.SvnCommit
    svnTagTask = at.bxm.gradleplugins.svntools.tasks.SvnTag
}

// 外部Gradleスクリプトの適用
def gradleScriptsDir = 'gradle-scripts'
apply from: "${gradleScriptsDir}/config-common.gradle" // プロジェクトの共通設定
apply from: "${gradleScriptsDir}/config-shared.gradle" // 共有プロジェクトの設定
apply from: "${gradleScriptsDir}/config-test.gradle" // テストの共通設定
apply from: "${gradleScriptsDir}/init-projects.gradle" // サブプロジェクトの初期化
apply from: "${gradleScriptsDir}/db-migration.gradle" // DBマイグレーション
apply from: "${gradleScriptsDir}/deploy-batch.gradle" // バッチデプロイ
apply from: "${gradleScriptsDir}/deploy-web.gradle" // Webデプロイ
